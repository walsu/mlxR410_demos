DESCRIPTION:
Multiple targets TMDD model 


[LONGITUDINAL]
input = {kel, ktp, kpt, Vc, Rs0, keps, kons, koffs, kouts, Rm0, kepm, konm, koffm}

PK:
; Dosemg: Dose injected into central compartment (mg/kg)
; MWlig=150000; molecular weight of ligand (Da) 
; (Dosemg*1e-3/MWlig)*1e9 ; Dose conversion (nmol/kg)

depot(target=Lctot, p=100/15/Vc)  ; Lc is a concentration (nM)

EQUATION:
odeType = stiff

Ksss  = (koffs+keps)/kons ; Steady state constant for soluble receptor (nM)
Kssm  = (koffm+kepm)/konm ; Steady state constant for membrane receptor (nM)
Vmaxm = Rm0*kepm
kins  = kouts*Rs0         ; Synthesis of soluble receptor (nM/day)

;Initial Conditions
Rstot_0 = Rs0

;Ordinary Differential Equations
Lc = 0.5*(Lctot-Rstot-Ksss+sqrt((Lctot-Rstot-Ksss)^2+4*Ksss*Lctot))
ddt_Lctot = -(kel+kpt)*Lc+ktp*Lt-(Rstot*keps*Lc)/(Ksss+Lc)-(Vmaxm*Lc)/(Kssm+Lc)
ddt_Rstot = kins-kouts*Rstot-(keps-kouts)*(Rstot*Lc)/(Ksss+Lc)
ddt_Lt    = -ktp*Lt + kpt*Lc

;Finding Amount of Free Receptor
Ps = Lctot-Lc-(Rm0*Lc)/(Kssm+Lc) ; Concentration of soluble complex (nM)
Rs = Rstot-Ps                    ; Concentration of soluble receptor (nM)
